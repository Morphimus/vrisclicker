Let's make a game!
	name:Vrisclicker
	by:Morphimus
	desc:vriska idle game lmao
	created:10/13/2021
	updated:10/13/2021
	version:0

Settings
	background:https://raw.githubusercontent.com/Morphimus/vrisclicker/main/background.png
	building cost increase:108%
	building cost refund:88%
	spritesheet:icons, 48 by 48, https://raw.githubusercontent.com/Morphimus/vrisclicker/main/icons.png
	stylesheet:https://raw.githubusercontent.com/Morphimus/vrisclicker/main/vrisStyle.txt

Layout
	*main
		contains:res, buttons
		*res
			contains:Resources
			class:fullWidth
		*buttons
			contains:Buttons
	*store
		contains:buildings, upgrades
		*buildings
			contains:BulkDisplay, Buildings
			header:Generators
			tooltip origin:left
			ps:show
		*upgrades
			contains:Upgrades
			header:Upgrades
			costs:hide
			names:hide

Buttons
	*luckButton
		name:Generate Luck
		desc:Click to m8ke all the luck! ALL OF IT!!!!!!!!
		on click:anim icon wobble
		on click:yield 1 luck
		icon:https://raw.githubusercontent.com/Morphimus/vrisclicker/main/luck.png
		no text
		class:bigButton hasFlares
		icon class:shadowed
		tooltip origin:top
		tooltip class:red

Resources
	*luck
		name:Luck|Luck
		desc:How much luck you've got.
		icon:icons[0,1] icons[1,2]
		class:noBackground
		show earned

	*boondollar|boondollars
		name:Boondollar|Boondollars
		desc:mad cash
		icon:icons[0,2]
		class:noBackground
		show earned
		hidden when 0
		

Shinies
	*aradiaTimeskip
		on click:log Woop
		movement:onRight moveLeft
		frequency: 10
		frequency variation: 5
		req:10 luck:earned
		icon:icons[2,2]
		class:bigButton
		on click:
			$boost=1
			if (chance(25%))
				$amount=max(7,random(luck:ps*60*1,luck:ps*60*3))*$boost
				toast Aradia the lucky bunny grants you<//><b>[$amount] luck</b>.
				yield $amount luck
			else
				$amount=1*$boost
				toast Aradia the lucky bunny grants you<//><b>[$amount] boondollar[s?$amount]</b>!
				yield $amount boondollars
			end
		end
		
Buildings
	*TEMPLATE
		on click:anim glow

	*eightball|eightballs
		name:Magic 8-ball|Magic 8-balls
		desc:You like to break these open sometimes.<//><b>Effect:</b><.>Produces 1 luck every 8 seconds.
		icon:icons[2,0]
		cost:8 luck
		on tick:yield 0.134 luck
		req:1 luck:earned

	*chump|chumps
		name:Mind controlled chump|Mind controlled chumps
		desc:Idiots that you exert your will over.<//><b>Effect:</b><.>Produces 1 boondollar every 10 seconds.
		icon:icons[2,1]
		cost:88 luck
		on tick:yield 0.1 boondollars
		req:mindControl

Upgrades
	*TEMPLATE
		on click:anim glow

	//building upgrades

	*mindControl
		name:Mind Control
		desc:Your mind is miiiiiiiine!!!!!!!!<//><b>Effect:</b><.>Unlocks resource generators.
		icon:icons[0,0]
		cost:8 luck
		req:8 luck:earned

	*stockExchange
		name:LOMAT Stock Exchange
		desc:Manipul8ing dum8 consorts has never 8een easier.<//><b>Effect:</b><.>Multiply Boondollar production by 8!!!!!!!!.
		icon:icons[0,2] icons[1,1]
		passive:multiply yield of boondollars by 8
		cost:10 boondollars
		req:5 boondollars:earned

	//luck upgrades

	*badBreak
		name:8ad 8r8k
		desc:Just my luck.<//><b>Effect:</b><.>Increases passive luck generation by like a fuckton, but makes active clicking almost worthless.
		icon:icons[2,0] icons[0,0]
		passive:multiply yield of luck by 800
		passive:multiply luck yield of luckButton by 0.0001
		cost:800 luck, 20 boondollars
		req:500 luck:earned

Achievements
	*achieveVriska
		name:Vriska!
		desc:Welcome to the 8ullshit.
		req:1 luck
		icon:icons[0,0] icons[2,1]

