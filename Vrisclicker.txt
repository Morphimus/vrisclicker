Let's make a game!
	name:Vrisclicker
	by:Morphimus
	desc:vriska idle game lmao
	created:10/13/2021
	updated:10/13/2021
	version:0

Settings
	background:https://raw.githubusercontent.com/Morphimus/vrisclicker/main/background.png
	building cost increase:115%
	building cost refund:88%
	spritesheet:icons, 48 by 48, https://raw.githubusercontent.com/Morphimus/vrisclicker/main/icons.png
		/* Icon columns are as follows
		0 BG panels (row 0 is special, beyond that in hemospectrum order)
		1 Character faces (same order as prev column)
		2 Resources
		3 Generators
		4 On-top things
		5 Romance symbols
		6 Borders
		7 Misc symbols
		*/
	stylesheet:https://raw.githubusercontent.com/Morphimus/vrisclicker/main/vrisStyle.txt

Layout
	*main
		contains:res, buttons
		*res
			contains:Resources
			class:fullWidth
		*buttons
			contains:Buttons
	*store
		contains:buildings, upgrades
		*buildings
			contains:BulkDisplay, Buildings
			header:Generators
			tooltip origin:left
			ps:show
		*upgrades
			contains:Upgrades
			header:Upgrades
			costs:hide
			names:hide

Buttons
	*luckButton
		name:Generate Luck
		desc:Click to m8ke all the luck! ALL OF IT!!!!!!!!
		on click:anim icon wobble
		on click:yield 1 luck
		icon:https://raw.githubusercontent.com/Morphimus/vrisclicker/main/luck.png
		no text
		class:bigButton hasFlares
		icon class:shadowed
		tooltip origin:top
		tooltip class:red

Resources
	*luck
		name:Luck|Luck
		desc:Your luck hasn't run out yet.
		icon:icons[2,0]
		class:noBackground
		show earned

	*boondollar|boondollars
		name:Boondollar|Boondollars
		desc:Mad cash.
		icon:icons[2,1]
		class:noBackground
		show earned
		hidden when 0

	*spotlight
		name:Spotlight
		desc:It's all a8out me!
		icon:icons[2,3]
		class:noBackground
		show earned
		hidden when 0

Shinies
	*aradiaTimeskip
		on click:log Woop
		movement:onRight moveLeft
		frequency: 55
		frequency variation: 20
		req:10 luck:earned
		icon:icons[0,1]
		on click:
			$boost=1
			if (chance(75%))
				$amount=max(7,random(luck:ps*60*1,luck:ps*60*5))*$boost
				toast Aradia does weird time shit, which ends up netting you<//><b>[$amount] luck</b>.
				yield $amount luck
			else
				$amount=luck:ps*60*$boost
				toast Aradia tick the clock forward when gets you<//><b>[$amount] luck</b>!
				yield $amount luck
			end
		end
		
Buildings
	*TEMPLATE
		on click:anim glow

	*eightball|eightballs
		name:Magic 8-ball|Magic 8-balls
		desc:You like to break these open sometimes.<//><b>Effect:</b><.>Produces 1 luck every 8 seconds.
		icon:icons[3,0]
		cost:8 luck
		on tick:yield 0.134 luck
		req:1 luck:earned

	*d8s|dice
		name:One d8|d8s
		desc:Roll the dice!!!!!!!!<//><b>Effect:</b><.>Produces 1 luck per second.
		icon:icons[3,1]
		cost:88 luck
		on tick:yield 1 luck
		req:80 luck:earned

	*chump|chumps
		name:Mind controlled chump|Mind controlled chumps
		desc:Idiots that you exert your will over.<//><b>Effect:</b><.>Produces 1 boondollar every 100 seconds.
		icon:icons[4,8] icons[2,1]
		cost:8000 luck
		on tick:yield 0.01 boondollars
		req:mindControl

Upgrades
	*TEMPLATE
		on click:anim glow

	//building upgrades


	*eightballUpgrade1
		name:8r8k! 8r8k! 8r8k!
		desc:Ouch! Broken 8-8alls suck to step on.<//><b>Effect:</b><.>Multiplies luck gained from eightballs by 2.
		icon:icons[4,4] icons[3,0]
		passive:multiply luck yield of eightballs by 2
		cost:12 luck
		req:10 luck:earned

	*eightballUpgrade2
		name:8-balls everywhere!
		desc:Your hive is absolutely lousy with the things<//><b>Effect:</b><.>Multiplies luck gained from eightballs by 2.
		icon:icons[4,5] icons[3,0]
		passive:multiply luck yield of eightballs by 2
		cost:88 luck
		req:40 luck:earned

	*diceUpgrade1
		name:Roll the dice!
		desc:Soon I will have all the luck! All of it!<//><b>Effect:</b><.>Multiplies luck gained from dice by 2.
		icon:icons[4,4] icons[3,1]
		passive:multiply luck yield of dice by 2
		cost:888 luck
		req:400 luck:earned

	*diceUpgrade2
		name:More dice less problems.
		desc:They always land on the num8ers I want.<//><b>Effect:</b><.>Multiplies luck gained from dice by 2.
		icon:icons[4,5] icons[3,1]
		passive:multiply luck yield of dice by 2
		cost:88888 luck
		req:40000 luck:earned

	*mindControl
		name:Mind Control
		desc:Your will is miiiiiiiine!!!!!!!!<//><b>Effect:</b><.>Unlocks resource generators.
		icon:icons[4,8] icons[1,8]
		cost:1000 luck
		req:1000 luck:earned

	*stockExchange
		name:LOMAT Stock Exchange
		desc:Manipul8ing dum8 consorts has never 8een easier.<//><b>Effect:</b><.>Multiply chumps' Boondollar production by 8!!!!!!!!.
		icon:icons[4,3] icons[2,1]
		passive:multiply boondollar yield of chumps by 8
		cost:1 boondollar, 8888 luck
		req:1 boondollars:earned


	//production upgrades

	*beginnersLuck
		name:Beginner's luck
		desc:Lucky!<//><b>Effect:</b><.>Multiplies passive luck generation by 2.
		icon:icons[2,0]
		passive:multiply yield of luck by 1.5
		cost:100 luck
		req:50 luck:earned

	*badBreak
		name:8ad 8r8k
		desc:Just my luck. ::::(<//><b>Effect:</b><.>Increases passive luck generation by like a fuckton, but makes active clicking almost worthless.
		icon:icons[2,0] icons[0,0]
		passive:multiply yield of luck by 800
		passive:multiply luck yield of luckButton by 0.001
		cost:8888 luck, 200 boondollars
		req:5000 luck:earned

	//Misc. upgrades//

	*egbert1
		name:Mess with John
		desc:8luh 8luh huge dork!
		icon:icons[1,13]
		cost:413 luck, 10 boondollars
		req:1000 luck, 1 boondollar

	*egbert2
		name:I'm June now.
		desc:8luh 8luh still a huge dork!
		icon:icons[4,13] icons[1,13] icons[0,13]
		cost:8888 luck
		req:egbert1, 4000 luck

	*egbert3
		name:D8 Egbert
		desc:Dating the main character is sure to propel you into the spotlight.<//><b>Effect:</b><.>Gain 1 spotlight per minute.
		icon:icons[5,5] icons[1,13]
		on tick:yield 0.0167 spotlight
		cost:413612 luck
		req:egbert1, 100000 luck

Achievements
	*achieveVriska
		name:Vriska!
		desc:Welcome to the 8ullshit.
		req:1 luck
		icon:icons[1,8]

	*achieveTrans
		name:Transed your gender.
		desc:In this house we respect June Egbert.
		req:egbert2
		icon:icons[0,13]
