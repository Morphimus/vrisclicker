Let's make a game!
	name:Vrisclicker
	by:Morphimus
	desc:vriska idle game lmao
	created:10/13/2021
	updated:10/14/2021
	version:0.9

Settings
	background:https://raw.githubusercontent.com/Morphimus/vrisclicker/main/background.png
	building cost increase:110%
	building cost refund:88%
	spritesheet:icons, 48 by 48, https://raw.githubusercontent.com/Morphimus/vrisclicker/main/icons.png
		/* Icon columns are as follows
		0 BG panels (row 0 is special, beyond that in hemospectrum order)
		1 Character faces (same order as prev column)
		2 Resources
		3 Generators
		4 On-top things
		5 Romance symbols
		6 Borders
		7 Misc symbols
		*/
	stylesheet:https://raw.githubusercontent.com/Morphimus/vrisclicker/main/vrisStyle.txt



/*Notes to self:

Icons I need to draw:
Mindfang's journal page
A red circle with a line through it
Floating egbert hand
Little floating aradiabot and little floating god tier aradia
Aradia face
Tavros in rocket chair
Tavros face
Consort


TODO BEFORE I DECLARE IT VERSION 1
Draw and fix all requisite icons
Move the whole john/june business later on, like maybe make the first one 413 million?
	also make the first egbert thing say like, wait why is Egbert here already this is anachronistic!
	and also don't deadname her :(

Change the journals to not have any of the text from homestuck itself.

Make a thing for killing off spidermom.
	Make it get rid of all of your spiderbait/make it useless


*/



Layout
	*main
		contains:res, buttons
		*res
			contains:Resources
			class:fullWidth
		*buttons
			contains:Buttons
	*store
		contains:buildings, upgrades
		*buildings
			contains:BulkDisplay, Buildings
			header:Generators
			tooltip origin:left
			ps:show
		*upgrades
			contains:Upgrades
			header:Upgrades
			costs:hide
			names:hide

Buttons
	*luckButton
		name:Generate Luck
		desc:Click to m8ke all the luck! ALL OF IT!!!!!!!!
		on click:anim icon wobble
		on click:yield 1 luck
		icon:https://raw.githubusercontent.com/Morphimus/vrisclicker/main/luck.png
		no text
		class:bigButton hasFlares
		icon class:shadowed
		tooltip origin:top
		tooltip class:red

Resources
	*luck
		name:Luck|Luck
		desc:Luckiest troll alive.
		icon:icons[2,0]
		class:noBackground
		show earned

	*boondollar|boondollars
		name:Boondollar|Boondollars
		desc:Mad cash.
		icon:icons[2,1]
		class:noBackground
		show earned
		hidden when 0

	*spotlight
		name:Spotlight
		desc:It's all a8out me!
		icon:icons[2,3]
		class:noBackground
		show earned
		hidden when 0
	*tavbux
		name:tavbux
		class:noBackground
		//always hidden//

Shinies
	*aradiaTimeskip
		on click:log Woop
		movement:onRight moveLeft growShrink
		frequency: 10
		frequency variation: 70
		req:1200 luck:earned
		icon:icons[7,0] icons[0,1]
		on click:
			$boost=1
			if (have aradiaBoost) $boost=8
			if (chance(50%))
				$amount=max(7,random(luck:ps*60*1,luck:ps*60*5))*$boost
				toast Aradia does weird time shit, which ends up netting you<//><b>[$amount] luck</b>.
				yield $amount luck
			else
				$amount=luck:ps*60*$boost
				toast Aradia ticks the clock forward which gets you<//><b>[$amount] luck</b>!
				yield $amount luck
			end
		end

/*	*egbertHand
		movement:anywhere fade
		frequency:40
		frequency variation:30
		icon:icons[1,13]
		req:egbert1
		on click:
			$amount=1
			toast You got [$amount] spotlight... For some reason?
			yield $amount spotlight
		end
I have commented out egbertHand */

	*tavros
		frequency:tavbux
		frequency variation:10
		movement:onBottom followMouseSlow
		icon:icons[0,2]
		req:tavrosIsHere
		on click:
			$amount=1
			toast Tavros gives you [$amount] boondollar[s?$amount]. Thanks Tavros.
			yield $amount boondollars
		end

Buildings
	*TEMPLATE
		on click:anim glow

	*eightball|eightballs
		name:Magic 8-ball|Magic 8-balls
		desc:You like to break these open sometimes.<//><b>Effect:</b><.>Produces 1 luck every 8 seconds.
		icon:icons[3,0]
		cost:8 luck
		cost increase:108%
		on tick:yield 0.134 luck
		req:1 luck:earned

	*dice|d8s
		name:Dice|D8s
		desc:Roll the dice!!!!!!!!<//><b>Effect:</b><.>Produces 1 luck per second.
		icon:icons[3,1]
		cost:88 luck
		on tick:yield 1 luck
		req:80 luck:earned

	*spiderbait
		name:Spiderbait
		desc:Well, you gotta feed spidermom........<//><b>Effect:</b><.>Produces 20 luck per second.
		icon:icons[2,2]
		cost:12000 luck
		on tick:yield 20 luck
		req:80000 luck:earned, mindControl

	*page|pages
		name:Mindfang's Journal|Journal pages
		desc:On the 14th 8ilunar perigee of the 2nd dim season's equinox........<//><b>Effect:</b><.>Produces 100 luck per second.<//>Hey did you know you can buy 50 of something at a time by holding down shift?
		icon:icons[3,3] icons[0,8]
		cost:1000000 luck
		cost increase:101%
		on tick:yield 100 luck
		req:10000000 luck:earned

	*chump|chumps
		name:Mind controlled chumps|Chumps
		desc:Idiots that you exert your will over.<//><b>Effect:</b><.>Produces 1 boondollar every 100 seconds.
		icon:icons[4,8] icons[2,1]
		cost:8000 luck
		on tick:yield 0.01 boondollars
		req:mindControl

	*fraymotif|fraymotifs
		name:Fraymotif|Fraymotifs
		desc:Battle tactics bought from consorts<//><b>Effect:</b><.>Produces 1000 luck per second.
		icon:icons[2,0] icons[0,0]
		cost:800 boondollars
		cost increase:112%
		on tick:yield 1000 luck
		req:413 boondollars:earned

	*level|levels
		name:God tier levels
		desc:I've got all the levels! All of them!!!!!!!!<//><b>Effect:</b><.>Produces 8888 luck per second.</><.>Produces 8 boondollars per second.</><.>Produces about 1 spotlight every 8 minutes or so.
		icon:icons[4,4] icons[1,0]
		cost:1 spotlight
		cost increase:105%
		on tick:yield 8888 luck
		on tick:yield 8 boondollars
		on tick:yield 0.002 spotlight
		req:godTier

Upgrades
	*TEMPLATE
		on click:anim glow

	//building upgrades//


	*eightballUpgrade1
		name:8r8k! 8r8k! 8r8k!
		desc:Ouch! Broken 8-8alls suck to step on.<//><b>Effect:</b><.>Multiplies luck gained from eightballs by 2.
		icon:icons[4,4] icons[3,0]
		passive:multiply luck yield of eightballs by 2
		cost:80 luck
		req:40 luck:earned

	*eightballUpgrade2
		name:8-balls everywhere!
		desc:Your hive is absolutely lousy with the things.<//><b>Effect:</b><.>Multiplies luck gained from eightballs by 2.
		icon:icons[4,5] icons[3,0]
		passive:multiply luck yield of eightballs by 2
		cost:800 luck
		req:400 luck:earned, eightballUpgrade1

	*eightballUpgrade3
		name:This might 8e too m8ny 8-8alls.
		desc:Running out of room to put them.<//><b>Effect:</b><.>Multiplies luck gained from eightballs by 2.
		icon:icons[4,6] icons[3,0]
		passive:multiply luck yield of eightballs by 2
		cost:80000 luck
		req:40000 luck:earned, eightballUpgrade2

	*eightballUpgrade4
		name:8-ballin'
		desc:You have stuffed 8-balls into as many chests on this meteor as you can, and yet you still need more room!<//><b>Effect:</b><.>Multiplies luck gained from eightballs by 4!
		icon:icons[4,7] icons[3,0]
		passive:multiply luck yield of eightballs by 4
		cost:800000 luck
		req:400000 luck:earned, eightballUpgrade3

	*eightballUpgrade5
		name:8 times the 8-balls!
		desc:My favor8 num8er! ::::)<//><b>Effect:</b><.>Multiplies luck gained from eightballs by 8!!!!!!!!
		icon:icons[4,2] icons[4,4] icons[3,0]
		passive:multiply luck yield of eightballs by 8
		cost:8000000 luck, 1 spotlight
		req:4000000 luck:earned, eightballUpgrade4, 1 spotlight:earned

	*eightballUpgrade6
		name:Crazy 8s
		desc:You're cr8zy for them.<//><b>Effect:</b><.>Multiplies luck gained from eightballs by 8!!!!!!!!
		icon:icons[4,2] icons[4,5] icons[3,0]
		passive:multiply luck yield of eightballs by 8
		cost:80000000 luck, 10 spotlight
		req:40000000 luck:earned, eightballUpgrade5, 10 spotlight:earned

	*eightballUpgrade7
		name:Gr8 times
		desc:8-ball? More like having a ball!<//><b>Effect:</b><.>Multiplies luck gained from eightballs by 8!!!!!!!!
		icon:icons[4,2] icons[4,6] icons[3,0]
		passive:multiply luck yield of eightballs by 8
		cost:800000000 luck, 20 spotlight
		req:400000000 luck:earned, eightballUpgrade6, 10 spotlight:earned

	*eightballUpgrade8
		name:Ultimate 8-balls
		desc:The pinnacle of 8-ball power... Or is it?<//><b>Effect:</b><.>Multiplies luck gained from eightballs by 808!!!!!!!! Wow!
		icon:icons[4,2] icons[4,7] icons[3,0]
		passive:multiply luck yield of eightballs by 808
		cost:80000000000 luck, 80 spotlight
		req:40000000000 luck:earned, eightballUpgrade7, 50 spotlight:earned

	*diceUpgrade1
		name:Roll the dice!
		desc:Soon I will have all the luck! All of it!<//><b>Effect:</b><.>Multiplies luck gained from dice by 2.
		icon:icons[4,4] icons[3,1]
		passive:multiply luck yield of dice by 2
		cost:888 luck
		req:400 luck:earned

	*diceUpgrade2
		name:More dice less problems.
		desc:They always land on the num8ers I want.<//><b>Effect:</b><.>Multiplies luck gained from dice by 2.
		icon:icons[4,5] icons[3,1]
		passive:multiply luck yield of dice by 2
		cost:88888 luck
		req:40000 luck:earned, diceUpgrade1

	*diceUpgrade3
		name:Keep rollin' rollin' rollin' rollin'
		desc:The more rolls I get the more goals are met!<//><b>Effect:</b><.>Multiplies luck gained from dice by 2.
		icon:icons[4,6] icons[3,1]
		passive:multiply luck yield of dice by 2
		cost:888888 luck
		req:400000 luck:earned, diceUpgrade2

	*diceUpgrade4
		name:Critical roll!
		desc:The more rolls I get the more goals are met!<//><b>Effect:</b><.>Multiplies luck gained from dice by 4!
		icon:icons[4,7] icons[3,1]
		passive:multiply luck yield of dice by 4
		cost:8888888 luck, 1 spotlight
		req:4000000 luck:earned, diceUpgrade3, 1 spotlight:earned

	*diceUpgrade5
		name:8 to the 8th!!!!!!!!
		desc:That's a lot of 8s........<//><b>Effect:</b><.>Multiplies luck gained from dice by 8!!!!!!!!
		icon:icons[4,2] icons[4,4] icons[3,1]
		passive:multiply luck yield of dice by 8
		cost:88888888 luck, 8 spotlight
		req:40000000 luck:earned, diceUpgrade4, 10 spotlight:earned

	*diceUpgrade6
		name:Troll bone dice
		desc:Mor8id, 8ut that's just how life is on Alternia.<//><b>Effect:</b><.>Multiplies luck gained from dice by 8!!!!!!!!
		icon:icons[4,2] icons[4,5] icons[3,1]
		passive:multiply luck yield of dice by 8
		cost:8888888800 luck, 20 spotlight
		req:4000000000 luck:earned, diceUpgrade5, 100 spotlight:earned

	*diceUpgrade7
		name:Buckets of dice
		desc:Get your mind out of the gutter!!!!!!!!<//><b>Effect:</b><.>Multiplies luck gained from dice by 8!!!!!!!!
		icon:icons[4,2] icons[4,6] icons[3,1]
		passive:multiply luck yield of dice by 8
		cost:88888888000 luck, 80 spotlight
		req:40000000000 luck:earned, diceUpgrade6

	*diceUpgrade8
		name:ALL THE D8s! ALL OF THEM!
		desc:The final dice upgr8de.<//><b>Effect:</b><.>Multiplies luck gained from dice by 808!!!!!!!!
		icon:icons[4,2] icons[4,7] icons[3,1]
		passive:multiply luck yield of dice by 808
		cost:888888880000 luck, 160 spotlight
		req:400000000000 luck:earned, diceUpgrade7

	*baitUpgrade1
		name:Like woolbeasts to the slaughter.
		desc:Damn, she eats so many trolls........<//><b>Effect:</b><.>Multiplies luck gained from spiderbait by 2.
		icon:icons[4,4] icons[2,2]
		passive:multiply luck yield of spiderbait by 2
		cost:800000 luck
		req:400000 luck:earned, mindControl

	*baitUpgrade2
		name:Body pile
		desc:Killing all these gogdamned trolls to feed my lusus. Fucking pain in my ass.<//><b>Effect:</b><.>Multiplies luck gained from spiderbait by 2.
		icon:icons[4,5] icons[2,2]
		passive:multiply luck yield of spiderbait by 2
		cost:8000000 luck
		req:4000000 luck:earned, baitUpgrade1

	*baitUpgrade3
		name:This sucks.
		desc:Spidermom sucks actually.<//><b>Effect:</b><.>Multiplies luck gained from spiderbait by 2.
		icon:icons[4,6] icons[2,2]
		passive:multiply luck yield of spiderbait by 2
		cost:80000000 luck
		req:40000000 luck:earned, baitUpgrade2

	*baitUpgrade4
		name:Feeding routine
		desc:Another day another 8atch of dead trolls.<//><b>Effect:</b><.>Multiplies luck gained from spiderbait by 4!
		icon:icons[4,7] icons[2,2]
		passive:multiply luck yield of spiderbait by 4
		cost:800000000 luck
		req:400000000 luck:earned, baitUpgrade3

	*baitUpgrade5
		name:Lusus lunch
		desc:Feeding this 8itch is soooooooo annoying!<//><b>Effect:</b><.>Multiplies luck gained from spiderbait by 8!
		icon:icons[4,2] icons[4,4] icons[2,2]
		passive:multiply luck yield of spiderbait by 8
		cost:8000000000 luck, 160 spotlight
		req:4000000000 luck:earned, baitUpgrade4, 1 spotlight:earned

	*baitUpgrade6
		name:Exhausting 8usiness
		desc:Feeding your lusus really takes a lot out of you.<//><b>Effect:</b><.>Multiplies luck gained from spiderbait by 8!
		icon:icons[4,2] icons[4,5] icons[2,2]
		passive:multiply luck yield of spiderbait by 8
		cost:80000000000 luck, 500 spotlight
		req:40000000000 luck:earned, baitUpgrade5

	*baitUpgrade7
		name:Sick of this
		desc:I am a8solutely FED UP WITH THIS SH8T!<//><b>Effect:</b><.>Multiplies luck gained from spiderbait by 8!
		icon:icons[4,2] icons[4,6] icons[2,2]
		passive:multiply luck yield of spiderbait by 64
		cost:800000000000 luck, 1000 spotlight
		req:400000000000 luck:earned, baitUpgrade6

	*pageUpgrade1
		name:Calignous riddle
		desc:The Orphaner poses a caliginous riddle like no other I've met. I am presuming him 8othered 8y jealousy, and it would 8e sickening if it were not so marvelously amusing. 8ut then, who 8ut royalty could have the finned cheek to show disdain for the manner in which his 8lack lover conducts her red conquests? Less has acceler8ted meeker than I to homicide, and the viol8tion would hold me aghast, again, if his misgivings did not complement his so endearing arsenal of qu8nt flaws. It is impossi8le to stifle this grin even now as I write.<//><b>Effect:</b><.>Multiplies luck gained from journal pages by 2.
		icon:icons[4,4] icons[3,3]
		passive:multiply luck yield of pages by 2
		cost:80000000 luck
		req:40000000 luck:earned

	*pageUpgrade2
		name:I expect nothing less.
		desc:He surely understands this as my maritime overlord, a superior while through gritting fangs he would concede the expanse of my plunder makes his seem hardly worthwhile to trou8le a map with good ink over. I know he understands. I will take what I want. I expect nothing less from Dualscar, and truly, less would offend me. Is it the crude 8lood of the suitors from which I have taken enjoyment recently? If his displeasure is with my 8lithe treatment of the social order then he has either not spent enough time in the warm company of my indifference, or is simply very stupid.<//><b>Effect:</b><.>Multiplies luck gained from journal pages by 2.
		icon:icons[4,5] icons[3,3]
		passive:multiply luck yield of pages by 2
		cost:800000000 luck
		req:400000000 luck:earned, pageUpgrade1

	*pageUpgrade3
		name:What is this look, my dear kismesis?
		desc:I saw the look he gave. He's so secure in knowing I can't feel what's in his mind he forgets the tr8torous ways of his own face. His little looks are words to me, interjections in our deliciously 8itter repartee. First a look as I summon a slave from the hold, with such ease 8etween my remarks. Why yes, Dualscar, they were the very slaves in your hold until 8ut this hour. Another ship deployed carelessly, languishing in strategic vulnera8ility. Is this not our routine? Our dance? What is this look, my dear kismesis? Is it shame? Envy? Contempt for what he knows will follow?<//><b>Effect:</b><.>Multiplies luck gained from journal pages by 2.
		icon:icons[4,6] icons[3,3]
		passive:multiply luck yield of pages by 2
		cost:8000000000 luck
		req:4000000000 luck:earned, pageUpgrade2

	*pageUpgrade4
		name:Not knowing is the point
		desc:I nod her over. She is fearful and it makes her prettier. He scoffs without a movement or sound. I know there is disgust feeding the shadows in his corner of my 8lock. At least prick her in the light, he surely thinks. Determine what vulgar hue she 8leeds 8efore persisting with your a8asement, Marquise. Do try to understand, Orphaner. Not knowing is the point, and if you truly understood this, your crusade against the Gam8lignants would not 8e among our Grand High8lood's most uproarious punchlines. (If only one truly needed to 8e so high to find it amusing!)<//><b>Effect:</b><.>Multiplies luck gained from journal pages by 4!
		icon:icons[4,7] icons[3,3]
		passive:multiply luck yield of pages by 4
		cost:80000000000 luck
		req:40000000000 luck:earned, pageUpgrade3

	*pageUpgrade5
		name:And so not knowing........
		desc:And so not knowing, I take her will, 8ut leave enough of it to enjoy her response. Her hands are in my service 8ut they still shake. They unfasten the first 8utton at my jacket's waist, clumsily. I have masked the line 8etween my puppeteering and her volition exquisitely, and her uncertainty over her own control fuels her fear. She unfastens the second 8utton, and 8etween the second and third, I make a casual remark to Dualscar, continuing our convers8tion. He does not respond.<//><b>Effect:</b><.>Multiplies luck gained from journal pages by 8!!!!!!!!
		icon:icons[4,2] icons[4,4] icons[3,3]
		passive:multiply luck yield of pages by 8
		cost:800000000000 luck, 80 spotlight
		req:400000000000 luck:earned, pageUpgrade4, 1 spotlight:earned

	*pageUpgrade6
		name:Aaaaaaaargh! So much text!
		desc:I look again at the face of my slave, imagining for a moment her mind is not an unguarded port to her every dread. I imagine I cannot feel her conviction that it's not merely a matter of whether she will 8e put to the irons, 8ut how hot they will 8e if she fails to please. Poor thing. Her horns make attractive shapes and pair themselves pleasantly amidst her violent snarls of hair. Her fingers, which I have lost track of, to my surprise have come 8etween the petticoat and my skin. The heat of her touch tells me the likely range for the color of her 8lood. I wouldn't have guessed it to look at her, not with her sign stripped. Her mouth opens slightly and I squint. Ah! Razor sharp, and none missing. Perfect. How disappointing it is to find quivering lips hiding dull teeth. I pause to consider. What will her fear 8ecome if I choose to show her mercy later? And even, in days, kindness? Will this 8e the red dalliance that 8ecomes fully flushed? Love demands my cunning just as my raids. If it is to 8e, she will never understand how thoroughly she was manipul8ted, her 8ody, her mind, her devotion.<//><b>Effect:</b><.>Multiplies luck gained from journal pages by 8!!!!!!!!</><.>These pages are fucking awful.
		icon:icons[4,2] icons[4,5] icons[3,3]
		passive:multiply luck yield of pages by 8
		cost:80000000000000 luck, 800 spotlight
		req:40000000000000 luck:earned, pageUpgrade5

	*pageUpgrade7
		name:Shameful literature
		desc:I remem8er Dualscar again. My distraction from our 8anter was momentarily a8solute, and I inquire into the shadows. 8ut he is gone.</></>Then go, my kismesis. Fume with the indign8tion I gave you. I can only pray it 8lackens our 8ond. I must know such exhi8itions agit8 him and hence why I 8other, otherwise it would 8e easy to dismiss him 8efore I partook. Let it 8e a gift of antagoniz8tion to you, my dear rival, on which you may 8rew pitch for me anew. And if it is true envy, a vermilion yearning I can't a8ide, then though it pains me it will 8e farewell.</></>Alas, it may 8e that I am too good at spurring h8. Too good, at least, for him. I only hope he is not so foolish as to tread a path of less torrid malice.</></>For if he does........<//><b>Effect:</b><.>Multiplies luck gained from journal pages by 8!!!!!!!!</><.>Have you actually been reading this shit?
		icon:icons[4,2] icons[4,7] icons[3,3]
		passive:multiply luck yield of pages by 8
		cost:800000000000000 luck, 8000 spotlight
		req:400000000000000 luck:earned, pageUpgrade6

	*chumpUpgrade1
		name:LOMAT Stock Exchange
		desc:Manipul8ing dum8 consorts has never 8een easier.<//><b>Effect:</b><.>Multiply chumps' Boondollar production by 8!!!!!!!!
		icon:icons[4,2] icons[2,1]
		passive:multiply boondollar yield of chumps by 8
		cost:1 boondollar, 8888 luck
		req:1 boondollars:earned

	*chumpUpgrade2
		name:Manipul8tion
		desc:Stupid 8roken game economy.<//><b>Effect:</b><.>Multiply chumps' Boondollar production by 8!!!!!!!!
		icon:icons[4,2] icons[2,1]
		passive:multiply boondollar yield of chumps by 8
		cost:1000 boondollars, 88888888 luck
		req:1000 boondollars:earned, chumpUpgrade1

	*chumpUpgrade3
		name:8oon8anks
		desc:Your virtual porkhollow is absolutely overflowing with boondollars!.<//><b>Effect:</b><.>Multiply chumps' Boondollar production by 8!!!!!!!!
		icon:icons[4,2] icons[2,1]
		passive:multiply boondollar yield of chumps by 8
		cost:100000 boondollars, 8888888800 luck
		req:100000 boondollars:earned, chumpUpgrade2

	*levelUpgrade1
		name:XP exploits
		desc:Gaining levels is so easy!<//><b>Effect:</b>:<.>Multiplies resource generation of god tier levels by 2.
		icon:icons[4,4] icons[1,0]
		passive:multiply yield of levels by 2
		cost:10000 boondollars
		req:20 levels

	//click upgrades//

	*advance1
		name:Arrow candy
		desc:Let's go to the next page, shall we?<//><b>Effect:</b><.>Makes clicking the luck button much more lucative. 1 point more lucrative, in fact.
		icon:icons[7,0]
		passive:increase luck yield of luckButton by 1
		cost:50 luck
		req:50 luckButton clicks

	*advance2
		name:More arrows
		desc:Another page another wage.<//><b>Effect:</b><.>Quintuples the luck gained from clicking the luck button.
		icon:icons[7,0]
		passive:multiply luck yield of luckButton by 5
		cost:1000 luck
		req:200 luckButton clicks, advance1

	*advance3
		name:Yet another arrow
		desc:What new batch of fortuity awaits us on the next page?<//><b>Effect:</b><.>Multiplies luck gained from clicking the luck button by 10.
		icon:icons[7,0]
		passive:multiply luck yield of luckButton by 10
		cost:100000 luck
		req:500 luckButton clicks, advance2

	*advance4
		name:Repetetive strain injury
		desc:Don't hurt your hand, seriously!<//><b>Effect:</b><.>Multiplies luck gained from clicking the luck button by 100.
		icon:icons[7,0]
		passive:multiply luck yield of luckButton by 100
		cost:10000000 luck, 1 spotlight
		req:1025 luckButton clicks, advance3, 1 spotlight:earned

	*advance5
		name:Next page please
		desc:Makes clicking the button worth it again!<//><b>Effect:</b><.>Multiplies luck gained from clicking the luck button by 100.
		icon:icons[7,0]
		passive:multiply luck yield of luckButton by 100
		cost:100000000 luck, 10 spotlight
		req:2000 luckButton clicks, advance4

	*advance6
		name:All of Homestuck
		desc:You clicked the luck button enough times to have clicked through all 8123 pages of Homestuck!<//><b>Effect:</b><.>Multiplies luck gained from clicking the luck button by 413.
		icon:icons[7,0] icons[7,2]
		passive:multiply luck yield of luckButton by 413
		cost:1000000000 luck, 32 spotlight
		req:8123 luckButton clicks, advance5

	//production upgrades//

	*luckUpgrade1
		name:Beginner's luck
		desc:Lucky!<//><b>Effect:</b><.>Multiplies all luck generation by 1.5.
		icon:icons[2,0]
		passive:multiply yield of luck by 1.5
		cost:1000 luck
		req:500 luck:earned

	*luckUpgrade2
		name:Lucky 8r8k
		desc:So much luck I am downright drowning in it.<//><b>Effect:</b><.>Double all luck generation.
		icon:icons[2,0]
		passive:multiply yield of luck by 2
		cost:1800000 luck
		req:1000000 luck:earned, luckUpgrade1

	*luckUpgrade3
		name:Your Luck is miiiiiiiine!
		desc:Stealing luck has never been so fun.<//><b>Effect:</b><.>Double all luck generation.
		icon:icons[2,0]
		passive:multiply yield of luck by 2
		cost:500000000 luck
		req:100000000 luck:earned, luckUpgrade2

	*badBreak
		name:8ad 8r8k
		desc:Just my luck. ::::(<//><b>Effect:</b><.>Increases passive luck generation by like a fuckton, but makes active clicking almost worthless.
		icon:icons[2,0] icons[0,0]
		passive:multiply yield of luck by 800
		passive:multiply luck yield of luckButton by 0.000001
		cost:8888888800000 luck, 888888 boondollars
		req:50000000 luck:earned, advance4

	*spotlightUpgrade1
		name:MeGaLoVania
		desc:F8CK YEAH!!!!!!!!<//><b>Effect:</b><.>Multiplies spotlight generation by 8!!!!!!!!
		icon:icons[2,2]
		passive:multiply yield of spotlight by 8
		cost:50 spotlight
		req:500 spotlight:earned


	//Misc. upgrades//

	*mindControl
		name:Mind Control
		desc:Your will is miiiiiiiine!!!!!!!!<//><b>Effect:</b><.>Unlocks resource generators.
		icon:icons[4,8] icons[1,8]
		cost:1000 luck
		req:1000 luck:earned

	*godTier
		name:godtier
		desc:finally briska get to got tiger<//><b>Effect:</b><.>You can no longer die unless the universe deems it a just or heroic death.</><.>Also unlocks resource generators.
		icon: icons[2,0] icons[0,14]
		cost:20 spotlight, 1000 boondollars, 1000000 luck
		req:1000000 luck:earned, 20 spotlight:earned, 1000 boondollars:earned

	*tavrosIsHere
		name:Tavros is here
		desc:Tavros will periodically show up to give you boondollars!</><#f00>WARNING!</#> He is annoying and cannot be removed.
		icon:icons[1,2] icons[0,2]
		cost:1 luck
		on earn:tavbux is 5
		//req:100 boondollars:earned//

	*egbert1
		name:Mess with John
		desc:8luh 8luh huge dork!
		icon:icons[1,13]
		cost:413 luck, 10 boondollars
		req:1000 luck:earned, 1 boondollar:earned

	*egbert2
		name:I'm June now.
		desc:8luh 8luh still a huge dork!
		icon:icons[4,13] icons[1,13] icons[0,13]
		cost:8888 luck, 100 boondollars
		req:egbert1, 5000 luck:earned

	*egbert3
		name:D8 Egbert
		desc:Dating the main character is sure to propel you into the spotlight.<//><b>Effect:</b><.>Gain 1 spotlight per minute.
		icon:icons[5,5] icons[1,13]
		on tick:yield 0.0167 spotlight
		cost:413612 luck, 413 boondollars
		req:egbert2, 300000 luck:earned
		//Note to self: make it so that instead of just giving you the spotlight, June shows up as a shiny you have to click for it. Maybe in addition?//

	*aradiaBoost
		name:Rel8tionship mended with Aradia
		desc:All bridges can be rebuilt with time! Even ones you burnt down OuO<//><b>Effect:</b><.>Octuples Aradia's timeskip effectiveness.
		icon:icons[1,1] icons[0,1]
		cost:100 spotlight
		req:200 spotlight:earned

Achievements
	*achieveVriska
		name:Vriska!
		desc:Welcome to the 8ullshit.
		req:1 luck
		icon:icons[1,8]

	*achieveTrans
		name:Transed your gender.
		desc:In this house we respect June Egbert.
		req:egbert2
		icon:icons[0,13]
